import com.diffplug.spotless.LineEnding

plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'com.diffplug.spotless' version "${spotless_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

spotless {
	lineEndings = LineEnding.UNIX

	java {
		removeUnusedImports()
		importOrder('java', 'javax', 'net.minecraft', 'com.hezaerd')
		leadingSpacesToTabs()
		trimTrailingWhitespace()
	}
}

repositories {

}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	implementation "com.google.code.gson:gson:${project.gson_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			
			pom {
				name = 'Wynn API'
				description = 'A Fabric library mod to wrap the Wynncraft API v3 and provide easy-to-use functionality for other mods.'
				url = 'https://github.com/hezaerd/wynn-api'

				licenses {
					license {
						name = 'MIT'
						url = 'https://opensource.org/licenses/MIT'
					}
				}

				developers {
					developer {
						id = 'hezaerd'
						name = 'Hezaerd'
						url = 'https://hezaerd.com/'
					}
				}

				scm {
					connection = 'scm:git:git://github.com/hezaerd/wynn-api.git'
					developerConnection = 'scm:git:ssh://github.com:hezaerd/wynn-api.git'
					url = 'https://github.com/hezaerd/wynn-api'
				}
			}
		}
	}

	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/hezaerd/wynn-api")
			credentials {
				username = System.getenv("USERNAME")
				password = System.getenv("TOKEN")
			}
		}
	}
}
