import com.diffplug.spotless.LineEnding

plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'com.diffplug.spotless' version "${spotless_version}"
	id 'maven-publish'
	id 'com.modrinth.minotaur' version "${minotaur_version}"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

spotless {
	lineEndings = LineEnding.UNIX

	java {
		removeUnusedImports()
		importOrder('java', 'javax', 'net.minecraft', 'com.hezaerd')
		leadingSpacesToTabs()
		trimTrailingWhitespace()
	}
}

repositories {

}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	implementation "com.google.code.gson:gson:${project.gson_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			
			pom {
				name = project.archives_base_name
				description = 'A Fabric library mod to wrap the Wynncraft API v3 and provide easy-to-use functionality for other mods.'
				url = 'https://github.com/hezaerd/wapi'

				licenses {
					license {
						name = 'MIT'
						url = 'https://opensource.org/licenses/MIT'
					}
				}

				developers {
					developer {
						id = 'hezaerd'
						name = 'Hezaerd'
						url = 'https://hezaerd.com/'
					}
				}

				scm {
					connection = 'scm:git:git://github.com/hezaerd/wapi.git'
					developerConnection = 'scm:git:ssh://github.com:hezaerd/wapi.git'
					url = 'https://github.com/hezaerd/wapi'
				}
			}
		}
	}

	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/hezaerd/wapi")
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = modrinth_project_id
	versionNumber = "${version}+${minecraft_version}"
	versionType = version_type
	uploadFile = remapJar
	changelog = generateChangelog(project)
	gameVersions = [minecraft_version]
	loaders = ["fabric"]
	dependencies {
		required.project "fabric-api"
	}
	
	syncBodyFrom = rootProject.file("README.md").text
}

static String generateChangelog(Project project) {
	def changelogFile = project.rootProject.file("changelog.md")
	if (!changelogFile.exists()) {
		println "WARNING: changelog.md file not found at ${changelogFile.absolutePath}. No changelog will be generated."
		return "No changelog available."
	}
	
	def currentVersion = project.version.toString()
	def changelogContent = changelogFile.text
	def changelogBuilder = new StringBuilder()
	boolean inCurrentVersionSection = false

	def versionHeaderPattern = ~/##\s*(v?${currentVersion.replace('.', '\\.')})/
	
	changelogContent.eachLine { line ->
		if (line.matches(versionHeaderPattern)) {
			inCurrentVersionSection = true
			return
		} else if (line.startsWith("## ") && inCurrentVersionSection) {
			inCurrentVersionSection = false
		}

		if (inCurrentVersionSection) {
			changelogBuilder.append(line).append("\n")
		}
	}

	def finalChangelog = changelogBuilder.toString().trim()
	if (finalChangelog.isEmpty()) {
		println "WARNING: No changelog entries found for version '${currentVersion}' in changelog.md."
		return "No changelog was specified for version ${currentVersion}."
	}
	return finalChangelog
}
